import QtQuick
import QtQuick.Layouts
import Quickshell
import qs
import qs.services
import qs.modules.common
import qs.modules.common.functions
import qs.modules.common.widgets
import qs.modules.common.widgets.widgetCanvas
import qs.modules.ii.background.widgets

AbstractBackgroundWidget {
    id: root
    configEntryName: "timers"
    hoverEnabled: true

    property real widgetWidth: 564
    property real cardSpacing: 12
    property real cardHeight: 120
    property real cardWidth: (widgetWidth - cardSpacing * 3) / 4
    property bool isVertical: root.configEntry.vertical ?? false

    implicitWidth: row.implicitWidth
    implicitHeight: row.implicitHeight

    property bool editingAlarm: false
    LazyLoader {
        active: root.editingAlarm
        component: AlarmEditorWindow {
            screen: Quickshell.screens.find(s => s.name === root.screenName) ?? Quickshell.screens[0]
            onDismissed: root.editingAlarm = false
        }
    }

    // The alarm card's text: its time, and when (or its state).
    readonly property date alarmDate: new Date(TimerService.alarmAt * 1000)
    readonly property string alarmWhen: {
        if (TimerService.alarmRinging) return Translation.tr("Ringing");
        if (TimerService.alarmSnoozeUntil > 0)
            return Translation.tr("Snoozed until %1").arg(Qt.formatTime(new Date(TimerService.alarmSnoozeUntil * 1000), Config.options.time.format));
        if (TimerService.alarmAt <= 0) return Translation.tr("Alarm");
        if (!TimerService.alarmEnabled) return Translation.tr("Off");
        if (TimerService.alarmDaily) return Translation.tr("Every day");
        const now = new Date(DateTime.clock.date);
        const tomorrow = new Date(now.getFullYear(), now.getMonth(), now.getDate() + 1);
        if (root.alarmDate.toDateString() === now.toDateString()) return Translation.tr("Today");
        if (root.alarmDate.toDateString() === tomorrow.toDateString()) return Translation.tr("Tomorrow");
        return Qt.formatDate(root.alarmDate, Config.options.time.dateFormat);
    }

    component TimerCard: Rectangle {
        id: timerCard
        property string icon: ""
        property string value: ""
        property string label: ""
        property bool running: false
        property string runningIcon: "pause"
        property int shape: MaterialShape.Shape.Cookie12Sided
        property color bgColor: Appearance.colors.colPrimaryContainer
        property color shapeColor: Appearance.colors.colPrimary
        property var onToggle: () => {}
        property var onReset: () => {}
        default property alias extraContent: extraSlot.data

        implicitWidth: root.cardWidth
        implicitHeight: root.cardHeight
        radius: Appearance.rounding?.verylarge ?? 30
        color: timerCard.bgColor

        StyledRectangularShadow {
            target: timerCard
            z: -2
            visible: Config.options.background.widgets.shadow
        }

        FastBlurred {
            anchors.fill: parent
            blurSource: root.wallpaperItem
            cardRadius: timerCard.radius
            tint: Appearance.colors.colLayer1
            tintOpacity: 0.55
            trackX: timerCard.x + root.x
            trackY: timerCard.y + root.y
            visible: Config.options.background.widgets.blurWidgets 
        }

        MouseArea {
            hoverEnabled: true
            anchors.fill: parent
            acceptedButtons: Qt.RightButton
            cursorShape: Qt.PointingHandCursor
            onClicked: timerCard.onReset()
        }

        ColumnLayout {
            anchors {
                fill: parent
                margins: 14
            }
            spacing: -4

            RowLayout {
                Layout.fillWidth: true
                spacing: 4

                Item { Layout.fillWidth: true }

                MaterialShapeWrappedMaterialSymbol {
                    shape: timerCard.shape
                    color: timerCard.shapeColor
                    colSymbol: Appearance.colors.colOnPrimary
                    text: timerCard.running ? timerCard.runningIcon : timerCard.icon
                    iconSize: 18
                    fill: 1
                    padding: 6
                    implicitWidth: 34
                    implicitHeight: 34

                    MouseArea {
                        hoverEnabled: true
                        anchors.fill: parent
                        cursorShape: Qt.PointingHandCursor
                        onClicked: timerCard.onToggle()
                    }
                }
            }

            Item { Layout.fillHeight: true }

            ColumnLayout {
                Layout.leftMargin: 2
                Layout.topMargin: -40
                spacing: -4
                StyledText {
                    text: timerCard.value
                    font.pixelSize: Appearance.font.pixelSize.large
                    font.weight: Font.Bold
                    font.features: { "tnum": 1 }
                    color: Appearance.colors.colOnPrimaryContainer
                }

                StyledText {
                    text: timerCard.label
                    font.pixelSize: Appearance.font.pixelSize.small
                    color: Appearance.colors.colOnPrimaryContainer
                    opacity: 0.6
                }
            }

            Item {
                id: extraSlot
                Layout.fillWidth: true
                Layout.preferredHeight: children.length > 0 ? 22 : 0
                Layout.topMargin: children.length > 0 ? 6 : 0
            }
        }
    }

    Grid {
        id: row
        columns: root.isVertical ? 1 : 4
        rows: root.isVertical ? 4 : 1
        spacing: root.cardSpacing

        Behavior on columns {
            NumberAnimation { duration: 300; easing.type: Easing.OutCubic }
        }

        // Pomodoro
        TimerCard {
            icon: TimerService.pomodoroBreak ? "coffee" : "visibility"
            value: TimerService.formatSeconds(TimerService.pomodoroSecondsLeft)
            label: TimerService.pomodoroBreak ? "Break" : "Focus"
            running: TimerService.pomodoroRunning
            bgColor: Appearance.colors.colTertiaryContainer
            shapeColor: Appearance.colors.colTertiary
            shape: MaterialShape.Shape.Flower
            onToggle: () => TimerService.togglePomodoro()
            onReset: () => TimerService.resetPomodoro()
        }

        // Stopwatch
        TimerCard {
            icon: "timer"
            value: TimerService.formatSeconds(TimerService.stopwatchTime / 100)
            label: "Stopwatch"
            running: TimerService.stopwatchRunning
            shape: MaterialShape.Shape.Sunny
            bgColor: Appearance.colors.colSecondaryContainer
            shapeColor: Appearance.colors.colSecondary
            onToggle: () => TimerService.toggleStopwatch()
            onReset: () => TimerService.stopwatchReset()
        }

        // Countdown
        TimerCard {
            icon: "hourglass_top"
            value: TimerService.formatSeconds(TimerService.countdownSecondsLeft)
            label: TimerService.countdownRinging ? Translation.tr("Ringing") : "Countdown"
            running: TimerService.countdownRunning || TimerService.countdownRinging
            runningIcon: TimerService.countdownRinging ? "notifications_off" : "pause"
            shape: MaterialShape.Shape.Bun
            bgColor: TimerService.countdownRinging ? Appearance.colors.colErrorContainer : Appearance.colors.colPrimaryContainer
            shapeColor: TimerService.countdownRinging ? Appearance.colors.colError : Appearance.colors.colPrimary
            onToggle: () => TimerService.toggleCountdown()
            onReset: () => TimerService.resetCountdown()

            RowLayout {
                anchors.fill: parent
                spacing: 4

                Repeater {
                    model: [1, 5]
                    delegate: Rectangle {
                        required property int modelData
                        Layout.fillWidth: true
                        Layout.fillHeight: true
                        radius: Appearance.rounding.full
                        color: ColorUtils.transparentize(Appearance.colors.colOnTertiaryContainer, 0.85)

                        StyledText {
                            anchors.centerIn: parent
                            text: "+" + modelData + "m"
                            font.pixelSize: Appearance.font.pixelSize.smallest
                            font.weight: Font.DemiBold
                            color: Appearance.colors.colOnPrimaryContainer
                        }

                        MouseArea {
                            hoverEnabled: true
                            anchors.fill: parent
                            cursorShape: Qt.PointingHandCursor
                            onClicked: TimerService.addCountdownMinutes(modelData)
                        }
                    }
                }
            }
        }

        // Alarm: the toggle switches it on/off; Edit opens the editor;
        // right-click deletes it.
        TimerCard {
            icon: "alarm_off"
            runningIcon: "alarm_on"
            value: TimerService.alarmAt > 0 ? Qt.formatTime(root.alarmDate, Config.options.time.format) : "--:--"
            label: TimerService.alarmLabel !== "" && !TimerService.alarmRinging ? `${root.alarmWhen} · ${TimerService.alarmLabel}` : root.alarmWhen
            running: TimerService.alarmEnabled || TimerService.alarmRinging || TimerService.alarmSnoozeUntil > 0
            shape: MaterialShape.Shape.Cookie9Sided
            bgColor: TimerService.alarmRinging ? Appearance.colors.colErrorContainer : Appearance.colors.colSecondaryContainer
            shapeColor: TimerService.alarmRinging ? Appearance.colors.colError : Appearance.colors.colSecondary
            onToggle: () => {
                if (TimerService.alarmRinging)
                    TimerService.dismissAlarm();
                else if (TimerService.alarmSnoozeUntil > 0)
                    TimerService.alarm.snoozeUntil = 0;
                else if (!TimerService.toggleAlarm())
                    root.editingAlarm = true;
            }
            onReset: () => TimerService.clearAlarm()

            RowLayout {
                anchors.fill: parent
                spacing: 4

                Repeater {
                    model: TimerService.alarmRinging
                        ? [{ text: Translation.tr("Snooze"), action: () => TimerService.snoozeAlarm() },
                           { text: Translation.tr("Dismiss"), action: () => TimerService.dismissAlarm() }]
                        : [{ text: TimerService.alarmAt > 0 ? Translation.tr("Edit") : Translation.tr("Set"), action: () => root.editingAlarm = true }]
                    delegate: Rectangle {
                        required property var modelData
                        Layout.fillWidth: true
                        Layout.fillHeight: true
                        radius: Appearance.rounding.full
                        color: ColorUtils.transparentize(Appearance.colors.colOnTertiaryContainer, 0.85)

                        StyledText {
                            anchors.centerIn: parent
                            text: parent.modelData.text
                            font.pixelSize: Appearance.font.pixelSize.smallest
                            font.weight: Font.DemiBold
                            color: Appearance.colors.colOnPrimaryContainer
                        }

                        MouseArea {
                            hoverEnabled: true
                            anchors.fill: parent
                            cursorShape: Qt.PointingHandCursor
                            onClicked: parent.modelData.action()
                        }
                    }
                }
            }
        }
    }

    Rectangle {
        id: toggleHandle
        width: 16
        height: 16
        radius: 6
        color: Appearance.colors.colOnPrimaryContainer
        anchors {
            left: parent.right
            bottom: parent.bottom
            margins: -6
        }
        opacity: root.containsMouse || toggleArea.containsMouse ? 0.7 : 0
        visible: opacity > 0 && !Config.options.background.widgetsLocked

        Behavior on opacity {
            NumberAnimation { duration: 150 }
        }

        MaterialSymbol {
            anchors.centerIn: parent
            text: "rotate_right"
            iconSize: 11
            color: Appearance.colors.colPrimaryContainer

            RotationAnimation on rotation {
                running: toggleArea.containsMouse
                from: 0
                to: 360
                duration: 1000
                loops: Animation.Infinite
            }
        }

        MouseArea {
            id: toggleArea
            anchors.fill: parent
            hoverEnabled: true
            cursorShape: Qt.PointingHandCursor
            onClicked: {
                root.isVertical = !root.isVertical
                root.configEntry.vertical = root.isVertical
            }
        }
    }
}