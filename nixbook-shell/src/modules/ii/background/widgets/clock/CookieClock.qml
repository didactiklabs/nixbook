pragma ComponentBehavior: Bound

import qs.services
import qs.modules.common
import qs.modules.common.widgets
import qs.modules.common.functions
import QtQuick
import QtQuick.Layouts
import Qt5Compat.GraphicalEffects
import Quickshell.Io

import qs.modules.ii.background.widgets.clock.dateIndicator
import qs.modules.ii.background.widgets.clock.minuteMarks

Item {
    id: root

    readonly property string clockStyle: Config.options.background.widgets.clock.style

    property real implicitSize: 230

    required property Item wallpaperItem
    property real originX: 0
    property real originY: 0
    property bool blurWidgets: Config.options.background.widgets.blurWidgets

    property color colShadow: Appearance.colors.colShadow
    property color colBackground: Appearance.colors.colWidgetCard
    property color colOnBackground: ColorUtils.mix(Appearance.colors.colSecondary, Appearance.colors.colPrimaryContainer, 0.15)
    property color colBackgroundInfo: ColorUtils.mix(Appearance.colors.colPrimary, Appearance.colors.colPrimaryContainer, 0.55)
    property color colHourHand: Appearance.colors.colPrimary
    property color colMinuteHand: Appearance.colors.colTertiary
    property color colSecondHand: Appearance.colors.colPrimary

    readonly property list<string> clockNumbers: DateTime.time.split(/[: ]/)
    readonly property int clockHour: parseInt(clockNumbers[0]) % 12
    readonly property int clockMinute: DateTime.clock.minutes
    readonly property int clockSecond: DateTime.clock.seconds
    // False while the clock is hidden behind windows or the screens are off
    // (ClockWidget): the second hand is unloaded, nothing redraws per second.
    property bool secondsVisible: true

    implicitWidth: implicitSize
    implicitHeight: implicitSize

    property bool useSineCookie: Config.options.background.widgets.clock.cookie.useSineCookie
    Item {
        id: cookieContainer
        anchors.fill: parent

        RotationAnimation on rotation {
            running: Config.options.background.widgets.clock.cookie.constantlyRotate
            duration: 30000
            easing.type: Easing.Linear
            loops: Animation.Infinite
            from: 360
            to: 0
        }

        WidgetShadow {
            target: cookieShapes
            visible: !root.blurWidgets && Config.options.background.widgets.shadow
        }

        Item {
            id: cookieShapes
            anchors.fill: parent

            Loader {
                id: sineCookieLoader
                anchors.fill: parent
                z: 0
                visible: !root.blurWidgets
                active: root.useSineCookie
                sourceComponent: SineCookie {
                    implicitSize: root.implicitSize
                    sides: Config.options.background.widgets.clock.cookie.sides
                    color: root.colBackground
                }
            }
            Loader {
                id: roundedPolygonCookieLoader
                anchors.fill: parent
                z: 0
                visible: !root.blurWidgets
                active: !root.useSineCookie
                sourceComponent: MaterialCookie {
                    implicitSize: root.implicitSize
                    sides: Config.options.background.widgets.clock.cookie.sides
                    color: root.colBackground
                }
            }
        }

        // Blurred wallpaper, masked by the cookie shape
        FastBlurred {
            id: cookieBlur
            anchors.fill: parent
            blurSource: root.wallpaperItem
            cardRadius: 0
            tint: Appearance.colors.colLayer1
            tintOpacity: 0.55
            trackX: root.originX + root.x
            trackY: root.originY + root.y
            visible: false
        }
        OpacityMask {
            anchors.fill: parent
            source: cookieBlur
            maskSource: root.useSineCookie ? sineCookieLoader.item : roundedPolygonCookieLoader.item
            z: 0
            visible: root.blurWidgets
        }
    }

    // Hour/minutes numbers/dots/lines
    MinuteMarks {
        anchors.fill: parent
        color: root.colOnBackground
    }

    // Stupid extra hour marks in the middle
    FadeLoader {
        id: hourMarksLoader
        anchors.centerIn: parent
        shown: Config.options.background.widgets.clock.cookie.hourMarks
        sourceComponent: HourMarks {
            implicitSize: 135 * (1.75 - 0.75 * hourMarksLoader.opacity)
            color: root.colOnBackground
            colOnBackground: ColorUtils.mix(root.colBackgroundInfo, root.colOnBackground, 0.5)
        }
    }

    // Number column in the middle
    FadeLoader {
        id: timeColumnLoader
        anchors.centerIn: parent
        shown: Config.options.background.widgets.clock.cookie.timeIndicators
        scale: 1.4 - 0.4 * timeColumnLoader.shown
        Behavior on scale {
            animation: Appearance.animation.elementResize.numberAnimation.createObject(this)
        }

        sourceComponent: TimeColumn {
            color: root.colBackgroundInfo
        }
    }

    // Minute hand
    FadeLoader {
        anchors.fill: parent
        z: 1
        shown: Config.options.background.widgets.clock.cookie.minuteHandStyle !== "hide"
        sourceComponent: MinuteHand {
            anchors.fill: parent
            clockMinute: root.clockMinute
            style: Config.options.background.widgets.clock.cookie.minuteHandStyle
            color: root.colMinuteHand
        }
    }

    // Hour hand
    FadeLoader {
        anchors.fill: parent
        z: item?.style === "hollow" ? 0 : 2
        shown: Config.options.background.widgets.clock.cookie.hourHandStyle !== "hide"
        sourceComponent: HourHand {
            clockHour: root.clockHour
            clockMinute: root.clockMinute
            style: Config.options.background.widgets.clock.cookie.hourHandStyle
            color: root.colHourHand
        }
    }

    // Second hand
    FadeLoader {
        id: secondHandLoader
        z: (Config.options.background.widgets.clock.cookie.secondHandStyle === "line") ? 2 : 3
        shown: root.secondsVisible && Config.options.time.secondPrecision && Config.options.background.widgets.clock.cookie.secondHandStyle !== "hide"
        anchors.fill: parent
        sourceComponent: SecondHand {
            id: secondHand
            clockSecond: root.clockSecond
            style: Config.options.background.widgets.clock.cookie.secondHandStyle
            color: root.colSecondHand
        }
    }

    // Center dot
    FadeLoader {
        z: 4
        anchors.centerIn: parent
        shown: Config.options.background.widgets.clock.cookie.minuteHandStyle !== "bold"
        sourceComponent: Rectangle {
            color: Config.options.background.widgets.clock.cookie.minuteHandStyle === "medium" ? root.colBackground : root.colMinuteHand
            implicitWidth: 6
            implicitHeight: implicitWidth
            radius: width / 2
        }
    }

    // Date
    FadeLoader {
        anchors.fill: parent
        shown: Config.options.background.widgets.clock.cookie.dateStyle !== "hide"

        sourceComponent: DateIndicator {
            color: root.colBackgroundInfo
            style: Config.options.background.widgets.clock.cookie.dateStyle
        }
    }
}