pragma ComponentBehavior: Bound
import qs.modules.common
import qs.modules.common.widgets
import qs.modules.common.functions
import qs.services
import qs.modules.common.models
import Qt5Compat.GraphicalEffects
import QtQuick
import QtQuick.Effects
import QtQuick.Layouts
import Quickshell
import Quickshell.Io
import Quickshell.Widgets
import Quickshell.Services.Mpris

Item {
    id: root

    property real cardWidth:     330
    property real buttonPadding: 5
    property real artMargin:     5

    property var player: MprisController.activePlayer

    // Scroll over the card switches the media source (app / browser / phone);
    // the media popup's chips pick one explicitly.
    WheelHandler {
        acceptedDevices: PointerDevice.Mouse | PointerDevice.TouchPad
        onWheel: (event) => {
            if (event.angleDelta.y !== 0)
                MprisController.cyclePlayer(event.angleDelta.y > 0 ? -1 : 1)
        }
    }

    property var    artUrl:      player?.trackArtUrl ?? ""
    property string trackTitle:  player?.trackTitle  ?? ""
    property string trackArtist: player?.trackArtist ?? ""
    property bool   isPlaying:   player?.isPlaying   ?? false
    property bool   hasTrack:    trackTitle.length > 0

    property string artDownloadLocation: Directories.coverArt
    property string artFileName:         Qt.md5(artUrl)
    property string artFilePath:         `${artDownloadLocation}/${artFileName}`
    property bool   artDownloaded:       false

    property string displayedArtFilePath: {
        if (!root.artDownloaded) return ""
        if (root.artUrl.startsWith("file://")) return root.artUrl
        return Qt.resolvedUrl(artFilePath)
    }

    property color artDominantColor: ColorUtils.mix(
        colorQuantizer?.colors[0] ?? Appearance.colors.colPrimary,
        Appearance.colors.colPrimaryContainer,
        0.8)

    property QtObject blendedColors: AdaptedMaterialScheme {
        color: root.artDominantColor
    }

    onArtFilePathChanged: {
        if (!root.artUrl || root.artUrl.length === 0) {
            root.artDominantColor = Appearance.m3colors.m3secondaryContainer
            root.artDownloaded = false
            return
        }

        if (root.artUrl.startsWith("file://")) {
            root.artDownloaded = true
            return
        }

        artDownloader.targetFile  = root.artUrl
        artDownloader.artFilePath = root.artFilePath
        root.artDownloaded = false
        artDownloader.running = true
    }

    Process {
        id: artDownloader
        property string targetFile:  root.artUrl
        property string artFilePath: root.artFilePath
        command: ["bash", "-c",
            `[ -f ${artFilePath} ] || curl -sSL '${targetFile}' -o '${artFilePath}'`]
        onExited: { root.artDownloaded = true }
    }

    ColorQuantizer {
        id: colorQuantizer
        source: root.displayedArtFilePath
        depth: 0
        rescaleSize: 1
    }

    visible:        root.hasTrack
    implicitWidth:  root.hasTrack ? root.cardWidth : 0
    implicitHeight: parent?.height ?? 46

    Behavior on implicitWidth {
        animation: Appearance.animation.elementMoveFast.numberAnimation.createObject(this)
    }

    StyledRectangularShadow {
        target: card
        visible: root.hasTrack
    }

    Rectangle {
        id: card
        anchors.fill:         parent
        anchors.topMargin:    Appearance.sizes.gapsOut
        anchors.bottomMargin: Appearance.sizes.gapsOut
        anchors.leftMargin:   Appearance.sizes.gapsOut
        anchors.rightMargin:  Appearance.sizes.gapsOut - 2
        radius: Appearance.rounding.normal
        color:  "transparent"

        layer.enabled: true
        layer.effect: OpacityMask {
            maskSource: Rectangle {
                width:  card.width
                height: card.height
                radius: card.radius
            }
        }

        Rectangle {
            anchors.fill: parent
            color: ColorUtils.applyAlpha(root.blendedColors.colLayer0, 1)
            z: 0
        }

        // Blur art 
        Image {
            id: blurredArt
            anchors.fill: parent
            source: root.displayedArtFilePath
            fillMode: Image.PreserveAspectCrop
            cache: false
            antialiasing: true
            asynchronous: true
            z: 1
            layer.enabled: true
            layer.effect: StyledBlurEffect {
                source: blurredArt
            }
        }

        // Overlay
        Rectangle {
            anchors.fill: parent
            color: ColorUtils.transparentize(root.blendedColors.colLayer0, 0.3)
            z: 2
        }

        RowLayout {
            width:  card.width
            height: card.height
            clip:   true
            spacing: 8
            z: 3

            // Art
            Rectangle {
                id: artRect
                Layout.alignment:  Qt.AlignVCenter
                Layout.leftMargin: root.artMargin + 2
                implicitWidth:     36
                implicitHeight:    36
                color:  ColorUtils.transparentize(root.blendedColors.colLayer1, 0.5)
                radius: Appearance.rounding.small

                // Source badge (only when there is a choice of players)
                Rectangle {
                    z: 2
                    visible: MprisController.players.length > 1 && root.player !== null
                    anchors { right: parent.right; bottom: parent.bottom }
                    width: 16; height: 16; radius: 8
                    color: root.blendedColors.colLayer0
                    IconImage {
                        anchors.centerIn: parent
                        width: 12; height: 12
                        visible: MprisController.playerIcon(root.player) !== ""
                        source: visible ? Quickshell.iconPath(MprisController.playerIcon(root.player), "audio-x-generic") : ""
                        asynchronous: true
                    }
                    MaterialSymbol {
                        anchors.centerIn: parent
                        visible: MprisController.playerIcon(root.player) === ""
                        text: MprisController.playerSymbol(root.player)
                        iconSize: 11
                        color: root.blendedColors.colOnLayer0
                    }
                }

                layer.enabled: true
                layer.effect: OpacityMask {
                    maskSource: Rectangle {
                        width:  artRect.width
                        height: artRect.height
                        radius: artRect.radius
                    }
                }

                StyledImage {
                    anchors.fill: parent
                    source: root.displayedArtFilePath
                    fillMode: Image.PreserveAspectCrop
                    cache: false
                    antialiasing: true
                    sourceSize.width:  artRect.width
                    sourceSize.height: artRect.height
                }
            }

            // Artist + Title
            ColumnLayout {
                Layout.fillWidth:  true
                Layout.fillHeight: true
                spacing: -2

                Item { Layout.fillHeight: true }

                StyledText {
                    Layout.fillWidth: true
                    text: root.trackArtist
                    font.pixelSize: Appearance.font.pixelSize.small - 2
                    color: root.blendedColors.colSubtext
                    elide: Text.ElideRight
                }

                StyledText {
                    Layout.fillWidth: true
                    text: StringUtils.cleanMusicTitle(root.trackTitle) || "Untitled"
                    font.pixelSize: Appearance.font.pixelSize.normal - 4
                    color: root.blendedColors.colOnLayer0
                    elide: Text.ElideRight
                    opacity: 0.7
                }

                Item { Layout.fillHeight: true }
            }

            // Buttons
            RowLayout {
                Layout.rightMargin: 4
                Layout.alignment:   Qt.AlignVCenter
                spacing: 1

                component DockIconButton: RippleButton {
                    property string iconName
                    property bool active: false
                    implicitWidth:  24
                    implicitHeight: 24
                    buttonRadius: implicitWidth / 2
                    colBackground:      ColorUtils.transparentize(root.blendedColors.colSecondaryContainer, 1)
                    colBackgroundHover: root.blendedColors.colSecondaryContainerHover
                    colRipple:          root.blendedColors.colSecondaryContainerActive
                    contentItem: MaterialSymbol {
                        anchors.centerIn: parent
                        horizontalAlignment: Text.AlignHCenter
                        text: parent.iconName
                        iconSize: Appearance.font.pixelSize.normal
                        fill: 1
                        color: parent.active ? root.blendedColors.colPrimary : root.blendedColors.colOnSecondaryContainer
                    }
                }

                // Previous
                DockIconButton {
                    iconName: "skip_previous"
                    downAction: () => root.player?.previous()
                }

                // Play / Pause
                RippleButton {
                    implicitWidth:  26
                    implicitHeight: 26
                    buttonRadius: root.isPlaying
                        ? Appearance.rounding.normal
                        : implicitWidth / 2
                    colBackground: root.isPlaying
                        ? root.blendedColors.colPrimary
                        : root.blendedColors.colSecondaryContainer
                    colBackgroundHover: root.isPlaying
                        ? root.blendedColors.colPrimaryHover
                        : root.blendedColors.colSecondaryContainerHover
                    colRipple: root.isPlaying
                        ? root.blendedColors.colPrimaryActive
                        : root.blendedColors.colSecondaryContainerActive
                    downAction: () => root.player?.togglePlaying()
                    contentItem: MaterialSymbol {
                        anchors.centerIn: parent
                        horizontalAlignment: Text.AlignHCenter
                        text: root.isPlaying ? "pause" : "play_arrow"
                        iconSize: Appearance.font.pixelSize.large
                        fill: 1
                        color: root.isPlaying
                            ? root.blendedColors.colOnPrimary
                            : root.blendedColors.colOnSecondaryContainer
                        Behavior on color {
                            animation: Appearance.animation.elementMoveFast.colorAnimation.createObject(this)
                        }
                    }
                }

                // Next
                RippleButton {
                    implicitWidth:  28
                    implicitHeight: 28
                    colBackground:      ColorUtils.transparentize(root.blendedColors.colSecondaryContainer, 1)
                    colBackgroundHover: root.blendedColors.colSecondaryContainerHover
                    colRipple:          root.blendedColors.colSecondaryContainerActive
                    downAction: () => root.player?.next()
                    contentItem: MaterialSymbol {
                        anchors.centerIn: parent
                        horizontalAlignment: Text.AlignHCenter
                        text: "skip_next"
                        iconSize: Appearance.font.pixelSize.large
                        fill: 1
                        color: root.blendedColors.colOnSecondaryContainer
                    }
                }

                // Shuffle / repeat / bring the player up (when supported)
                DockIconButton {
                    visible: root.player?.shuffleSupported ?? false
                    iconName: (root.player?.shuffle ?? false) ? "shuffle_on" : "shuffle"
                    active: root.player?.shuffle ?? false
                    downAction: () => MprisController.toggleShuffle(root.player)
                }
                DockIconButton {
                    visible: root.player?.loopSupported ?? false
                    iconName: MprisController.loopIcon(root.player)
                    active: (root.player?.loopState ?? MprisLoopState.None) !== MprisLoopState.None
                    downAction: () => MprisController.cycleLoop(root.player)
                }
                DockIconButton {
                    id: dockMoreButton
                    visible: root.player !== null
                    iconName: "more_horiz"
                    downAction: () => dockActionsMenu.toggle()
                    MediaActionsMenu {
                        id: dockActionsMenu
                        anchorItem: dockMoreButton
                        player: root.player
                        holdsDock: true
                    }
                }
            }
        }
    }
}
